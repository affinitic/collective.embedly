*** Settings ***

Library  Selenium2Library  timeout=15  implicit_wait=0.1

Resource  plone/act/keywords.txt

Variables  plone/app/testing/interfaces.py

Suite Setup  Open Browser and login

Suite Teardown  Close All Browsers


*** Variables ***

${T_NO_EMBEDLY} =  No Embedly Block
${L_NO_EMBEDLY} =  http://www.youtube.com/watch?v=L1NPLlhFTVk
${B_NO_EMBEDLY} =  <a class="external-link" href="${L_NO_EMBEDLY}">${T_NO_EMBEDLY}</a>

${T_NO_VALID} =  No Valid Block
${L_NO_VALID} =  http://not.valid.url/11
${B_NO_VALID} =  <a class="external-link embedlylink" href="${L_NO_VALID}">${T_NO_VALID}</a>

${T_FLICKR} =  Embedly Flickr Block
${L_FLICKR} =  http://www.flickr.com/photos/123.jpg
${B_FLICKR} =  <a class="external-link embedlylink" href="${L_FLICKR}">${T_FLICKR}</a>

${T_YOUTUBE} =  Embedly Youtube Block
${L_YOUTUBE} =  http://www.youtube.com/watch?v=L1NPLlhFTVk
${B_YOUTUBE} =  <a class="external-link embedlylink" href="${L_YOUTUBE}">${T_YOUTUBE}</a>

${TINY_MCE} =  document.getElementById('text_ifr')
${INSERT_MCE} =  ${TINY_MCE}.contentDocument.body.innerHTML
${E_BLOCK} =  css=div.embed


*** Test Cases ***

Test Embedly Video
    Create page with title Page Embedly Video and text ${B_YOUTUBE}
    Page Should Not Contain  ${T_YOUTUBE}
    Page Should Contain Element  ${E_BLOCK}

Test Embedly Image
    Create page with title Page Embedly Image and text ${B_FLICKR}
    Page Should Not Contain  ${T_FLICKR}
    Page Should Contain Element  ${E_BLOCK}

Test Embedly No Valid URL
    Create page with title Page Embedly No Valid URL and text ${B_NO_VALID}
    Page Should Contain  ${T_NO_VALID}
    Page Should Not Contain Element  ${E_BLOCK}

Test No Embedly
    Create page with title Page No Embedly and text ${B_NO_EMBEDLY}
    Page Should Contain  ${T_NO_EMBEDLY}
    Page Should Not Contain Element  ${E_BLOCK}


*** Keywords ***

Create page with title ${title} and text ${text}
    Open Add New Menu
    Click Link  link=Page
    Wait Until Page Contains Element  title
    Input Text  title  ${title}
    Wait Until Page Contains Element  dom=${TINY_MCE}
    Execute JavaScript  ${INSERT_MCE}='${text}'
    Click button  Save
    Page Should Contain  Changes saved.

Open Browser and login
    Open browser  ${PLONE_URL}
    Log in as site owner
    Goto homepage